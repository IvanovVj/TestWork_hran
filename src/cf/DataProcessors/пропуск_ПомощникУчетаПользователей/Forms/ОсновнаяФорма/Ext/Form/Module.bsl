
#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура КомандаСгенерироватьИдентификаторПропуска(Команда)
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.пропуск_ИдентификаторПропуска > 0 Тогда
		ДополнительныеПрараметры = Новый Структура;
		ДополнительныеПрараметры.Вставить("Пользователь", ТекущиеДанные.Ссылка);
		ОписаниеОповещения =
			Новый ОписаниеОповещения(
				"КомандаСгенерироватьИдентификаторПропускаПродолжение",
				ЭтотОбъект,
				ДополнительныеПрараметры);
		Текст = "Идентификатор пропуска уже задан. Создать новый?";
		ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе
		СгенерироватьИдентификаторПропускаНаСервере(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеОбработчики

&НаКлиенте
Процедура КомандаСгенерироватьИдентификаторПропускаПродолжение(РезультатВопроса, ДополнительныеПрараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СгенерироватьИдентификаторПропускаНаСервере(ДополнительныеПрараметры.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СгенерироватьИдентификаторПропускаНаСервере(Пользователь)
	
	пропуск_РаботаСПропусками.СгенерироватьИдентификаторПропускаПользователя(Пользователь);
	
	Элементы.СписокПользователей.Обновить();
	
	Элементы.СписокПользователей.ТекущаяСтрока = Пользователь;
	
КонецПроцедуры

#КонецОбласти
